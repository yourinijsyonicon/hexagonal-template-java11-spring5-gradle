buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "io.spring.gradle:dependency-management-plugin:1.0.11.RELEASE"
    }
}

plugins {
    id "org.sonarqube" version "3.5.0.2730"
    id "org.owasp.dependencycheck" version "8.0.2"
    id "jacoco"
}

task printVersion {
    doLast {
        println project.version
    }
}

def jacocoExclusion = ["**/be/yonicon/hexagonal/rdbms/jooq/**",
                       "**/be/yonicon/hexagonal/test/**",
                       "**/be/yonicon/hexagonal/sanity/**",
                       "**/be/yonicon/hexagonal/**/config/**"]

allprojects {
    repositories {
        mavenCentral()
    }

    group = 'be.yonicon.template'
    version = System.getenv('BITBUCKET_COMMIT') != null ? System.getenv('BITBUCKET_COMMIT').substring(0, 7) : 'local'
}

subprojects {
    apply plugin: 'maven-publish'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java-library'

    sourceCompatibility = JavaVersion.VERSION_11
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from(components.java)
            }
        }
    }

    def test = tasks.named("test") {
        useJUnitPlatform {
            excludeTags "slow-test"
        }
        testLogging {
            events "passed", "skipped", "failed"
        }
        jacoco {
            excludes = jacocoExclusion
        }
    }

    def slowTest = tasks.register("slowTest", Test) {
        useJUnitPlatform {
            includeTags "slow-test"
        }
        testLogging {
            events "passed", "skipped", "failed"
        }
        jacoco {
            excludes = jacocoExclusion
        }
        shouldRunAfter test
    }

    dependencyManagement {
        imports {
            mavenBom 'org.springframework.boot:spring-boot-dependencies:2.7.8'
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:2021.0.5"
        }
        dependencies {
            dependency 'net.logstash.logback:logstash-logback-encoder:7.2'
            dependency 'io.springfox:springfox-boot-starter:3.0.0'
        }
    }

    dependencies {
        implementation 'org.slf4j:slf4j-api'
        testImplementation 'org.junit.jupiter:junit-jupiter'
        testImplementation 'org.assertj:assertj-core'
    }
}